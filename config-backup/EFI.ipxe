#!ipxe
cpuid --ext 29 && set arch x64 || set arch x86
cpuid --ext 29 && set archl amd64 || set archl i386
cpair --foreground 6 0
echo
echo Presidio's PXE Server 
echo
cpair --foreground 1 0
echo !!!!!! RIATTIVATE SECURE BOOT ZIOPERA  !!!!!!
echo
echo
sleep 3
echo Configurazione della scheda di rete..  
sleep 5 

dhcp

:start-menu
set cls:hex 1b:5b:4a
set cls ${cls:string}
set space:hex 20:20
set space ${space:string}
set sp:hex 20
set sp ${sp:string}

menu Presidio Boot Server
item --gap --            ---------------------------------------- Utility: -----------------------------------------
	item acronis         ${space} Acronis True Image
	item gparted         ${space} Gparted Live (Partition Manager)
	item nixos-disk-destroyer ${space} Disk Destroyer
	item nixos-hardware  ${space} Hardware Tester
	item clonezilla      ${space} Clonezilla Live
	item netbootxyz      ${space} Internet Images - Netboot XYZ
	item localboot	     ${space} Boot from local storage
item --gap --            --------------------------------------------- Altro: --------------------------------------------
	item view            ${space} Network setting
	item config          ${space} Configure iPXE
	item shell           ${space} iPXE shell
	item reboot          ${space} Reboot
  	item poweroff        ${space} Power off
	item exit            ${space} Exit
isset ${target} && set timeout 0 || set timeout 20000
choose --timeout ${timeout} --default ${target} target && goto ${target} || goto failed
echo ${cls}
goto start-menu

:acronis
sanboot --no-describe http://192.168.55.252:9080/pxe-server/images/acronispe.iso || goto failed

:gparted
kernel http://192.168.55.252:9080/pxe-server/images/gparted/live/vmlinuz boot=live config components locales=it_IT.UTF-8 keyboard-layouts=it username=user union=overlay noswap noeject vga=788 fetch=http://192.168.55.252:9080/pxe-server/images/gparted/live/filesystem.squashfs gparted_mode=0 
initrd http://192.168.55.252:9080/pxe-server/images/gparted/live/initrd.img 
boot || goto failed

:nixos-disk-destroyer
kernel http://192.168.55.252:9080/pxe-server/images/nixos-disk-destroyer/bzImage init=/nix/store/b84kr2czivcv56krfyqikbxh1swy81xj-nixos-system-nixos-disk-destroyer-24.11beta708350.1d95cb5/init initrd=initrd nohibernate loglevel=4 ${cmdline}
initrd http://192.168.55.252:9080/pxe-server/images/nixos-disk-destroyer/initrd
boot

:nixos-hardware
set nixos_url http://192.168.55.252:9080/pxe-server/images/nixos-hardware-tester/nixos-hardware.iso
kernel ${nixos_url}/boot/bzImage  
initrd ${nixos_url}/boot/initrd
boot || goto failed

:clonezilla
kernel http://192.168.55.252:9080/pxe-server/images/clonezilla/vmlinuz
initrd http://192.168.55.252:9080/pxe-server/images/clonezilla/initrd.img
imgargs vmlinuz boot=live config noswap nolocales edd=on nomodeset ocs_live_run="ocs-live-general" ocs_live_extra_param="" keyboard-layouts= ocs_live_batch="no" locales=it_IT.UTF-8 vga=788 nosplash noprompt fetch=http://192.168.55.252:9080/pxe-server/images/clonezilla/filesystem.squashfs
boot

:netbootxyz
chain --autofree http://boot.netboot.xyz/ipxe/netboot.xyz.efi

:view
isset ${memsize} && echo ${manufacturer} | ${product} | ${platform} | ${arch} | ${memsize} MiB || echo ${manufacturer} | ${product} | ${platform} | ${arch}
echo -- ------------------------------------------------------------------------
isset ${proxydhcp/filename} && echo The network settings obtained from the a proxyDHCP server || set man-flag 1
#isset ${netX.dhcp/filename} && echo The network settings obtained from the a DHCP server || set man-flag 2
isset ${netX.dhcp/dhcp-server} && echo The network settings obtained from the a DHCP server || set man-flag 3
isset ${man-flag} || echo The network settings set manually
echo
echo ${ifname} is a  ${chip} ${pci/${busloc}.0.2}:${pci/${busloc}.2.2}
echo mac  ${sp}: ${mac}
echo ip  ${space}: ${ip}
echo mask		 : ${netmask}
echo gw  ${space}: ${gateway}
echo dns    ${sp}: ${dns}
echo tftp		 : ${next-server}
echo dhcp		 : ${dhcp-server}
echo http		 : ${boot-url}
echo file		 : ${filename}
echo
prompt Press any key to back to the menu ...
goto start-menu

:config
config

:shell
echo ${cls}
echo Type "help" to get the list commands.
echo Type "exit" to get the back to the menu.
echo
shell
goto start-menu

:reboot
reboot --warm || goto failed
goto start-menu

:poweroff
poweroff || goto failed
goto start-menu

:exit
echo Continuing BIOS boot order...
sleep 1
exit 0
